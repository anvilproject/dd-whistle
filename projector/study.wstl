// Research Study 
// The research study acts as the Umbrella that covers all details associated with
// a single study. Currently, we treat all of DS-Connect as one study. Patients 
// are tied to a study by way of the ResearchSubject. 

def StudyArtifact(artifact_type, artifact_label, artifact_url) {
    if (artifact_url?) {
        type: artifact_type;
        label: artifact_label;
        url: artifact_url;
    }
}

// Definition: Build the ResearchStudy resource
//
// Argument:
//  study - This is the full study object created by Whistler
//
def Study(study) {
    meta.tag[]: StudyMeta(study);
    meta.profile[]: "https://ncpi-fhir.github.io/ncpi-fhir-study-summary-ig/StructureDefinition/ncpi-research-study";

    title: study.title;
    description: study.desc;
    status: "completed"

    // We don't really have much info at this time, so creating a group with 
    // no information even subject count doesn't seem to make much sense 
    // EST 2023-01-25
    //enrollment[0]: Reference_Key_Identifier(study, "Group", study.id);
    identifier[0]: Key_Identifier(study, "ResearchStudy", study.id);
    identifier[0].use: "official";
    identifier[1].value (if study.accession?): study.accession;
    identifier[1].system (if study.accession?): "https://www.ncbi.nlm.nih.gov/projects/gap/cgi-bin/study.cgi?study_id=";

    keyword[].coding[].code: study.id;

    // Borrowed from INCLUDE, but should be suitable for most types of studies 
    // until we have a firm standard to use.
    relatedArtifact[] (if study.url?): StudyArtifact("documentation", "Study Website", study.url);
    relatedArtifact[] (if study.remote_access?): StudyArtifact("derived-from", study.remote_access.type, study.remote_access.url);
    relatedArtifact[] (if study.accession?): StudyArtifact("derived-from", "DbGaP", study.accession);

    resourceType: "ResearchStudy";
}



// Description: Wrapper for all research studies in the dataset. For now, it's just the one
//
// Argument:
//  study - This is the full study object created by Whistler
def ProcessStudy(study) {
    out research_study: Study(study);
}
